SUMMARY = "Framework for layout and rendering of internationalized text"
DESCRIPTION = "Pango is a library for laying out and rendering of text, \
with an emphasis on internationalization. Pango can be used anywhere \
that text layout is needed, though most of the work on Pango so far has \
been done in the context of the GTK+ widget toolkit. Pango forms the \
core of text and font handling for GTK+-2.x."
HOMEPAGE = "http://www.pango.org/"
BUGTRACKER = "http://bugzilla.gnome.org"
SECTION = "libs"
LICENSE = "LGPL-2.0-or-later"

LIC_FILES_CHKSUM = "file://COPYING;md5=3bf50002aefd002f49e7bb854063f7e7"

GNOMEBASEBUILDCLASS = "meson"

inherit gnomebase gtk-doc ptest-gnome upstream-version-is-even gobject-introspection

SRC_URI += "file://run-ptest \
            file://insensitive-diff.patch \
            file://CVE-2019-1010238.patch \
            "

SRC_URI[archive.md5sum] = "deb171a31a3ad76342d5195a1b5bbc7c"
SRC_URI[archive.sha256sum] = "1d2b74cd63e8bd41961f2f8d952355aa0f9be6002b52c8aa7699d9f5da597c9d"

DEPENDS = "glib-2.0 glib-2.0-native fontconfig freetype virtual/libiconv cairo \
           harfbuzz fribidi gobject-introspection gobject-introspection-native"

PACKAGECONFIG ??= "${@bb.utils.filter('DISTRO_FEATURES', 'x11', d)}"
PACKAGECONFIG[x11] = ",,virtual/libx11 libxft"

#20221202 fix: ERROR: Unknown options: "docs"
#GIR_MESON_OPTION = 'gir'
GIR_MESON_OPTION = ''
EXTRA_OEMESON:append:class-target = " -Dgir=true -Denable_docs=false"
EXTRA_OEMESON:append:class-native = " -Dgir=false -Denable_docs=false"
EXTRA_OEMESON:remove = "-Ddocs=false"

LEAD_SONAME = "libpango-1.0*"
LIBV = "1.8.0"

FILES:${PN} = "${bindir}/* ${libdir}/libpango*${SOLIBS}"
FILES:${PN}-dev += "${libdir}/pango/${LIBV}/modules/*.la"

RDEPENDS:${PN}-ptest += "liberation-fonts cantarell-fonts"

RPROVIDES:${PN} += "pango-modules pango-module-indic-lang \
                    pango-module-basic-fc pango-module-arabic-lang"

BBCLASSEXTEND = "native nativesdk"

do_install:append () {
	if [ "${PTEST_ENABLED}" != "1" ]; then
		rm -rf ${D}${libexecdir}/installed-tests ${D}${datadir}/installed-tests
                rmdir --ignore-fail-on-non-empty ${D}${libexecdir} ${D}${datadir}
	fi
}
